#! /usr/bin/fish

function usage
	set_color --bold
	echo "Usage:" (set_color normal)\
	(basename (status -f)) " [-c <device>] [-d <device>] [-h] [-l] [-t <device>]"
	echo "    -c/--connect <device>           connect to <device>"
	echo "    -d/--disconnect                 disconnect"
	echo "    -h/--help                       show this help"
	echo "    -l/--list                       list known devices"
	echo "    -t/--tethering                  tethering via bluetooth"
end

if test (count $argv) -eq 0
	usage
	exit 1
end

argparse "c/connect=" "d/disconnect" "h/help" "l/list" "t/tethering=" -- $argv
or begin
	usage
	exit 1
end

if test -n "$_flag_help"
	usage
	exit
end

# global variables containing the device mac and name
set device_mac
set device_name

function get_mac --argument-names device
	for line in (bluetoothctl devices | grep -i "$device")
		set device_mac (echo $line | cut -d ' ' -f 2)
		set device_name (echo $line | cut -d ' ' -f 3)
	end
	if test -z "line"
		echo "Unknown device $device. Aborting."
		exit 1
	end		
end

if test -n "$_flag_list"
	# print list of devices
	bluetoothctl devices
	exit
end

if test -n "$_flag_connect"
	get_mac $_flag_connect
	echo "Pairing with device $device_name."
	bluetoothctl -- power on
	bluetoothctl -- pair $device_mac
	bluetoothctl -- connect $device_mac
	exit
end

if test -n "$_flag_disconnect"
	for mac in (bluetoothctl devices Connected | cut -f 2 -d ' ')
		bluetoothctl -- disconnect $mac
	end
	bluetoothctl -- power off
	exit
end

if test -n "$_flag_tethering"
	get_mac $_flag_tethering
	echo "Tethering with device $device_name."
	# This will create a network interface
	# managed by /etc/systemd/network/30-bluetooth.network
	# https://wiki.archlinux.org/title/Android_tethering#Tethering_via_Bluetooth
	set interface (echo $device_mac | sed -e 's/:/_/g')
	dbus-send --system --type=method_call \
		--dest=org.bluez /org/bluez/hci0/dev_$interface \
		org.bluez.Network1.Connect string:'nap'
	exit
end
