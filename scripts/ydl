#! /usr/bin/fish

# video format (18)
set _flag_format 18
#set ydl youtube-dl
set ydl yt-dlp

function usage
	echo "Usage:"
	echo (basename (status -f)) "[-f format] [-h] [-i] [-l] [-m] [-n] [url]" 
	echo "    f/format: set the format."
	echo "    h/help: show this help."
	echo "    i/info: list available formats."
	echo "    l/list: download the whole list."
	echo "    m/music: download music only."
	echo "    n/noplay: don't play."
	exit 1
end

argparse "l/list" "f/format=" "i/info" "m/music" "h/help" "n/noplay" -- $argv
or usage

if test -n "$_flag_help"
	usage
end

if test -n "$argv[1]"
	set url $argv[1]
else
	set url (wl-paste)
end
set id ($ydl -q --no-playlist --get-id "$url")


cd /tmp/

if test -n "$_flag_list"
	if test -n "$_flag_music"
		$ydl -i -x --format $_flag_format -o '%(playlist_index)s.m4a' "$url"
	else
		$ydl -i --format $_flag_format  -o '%(playlist_index)s.mp4' "$url"
	end
else if test -n "$_flag_info"
	$ydl --list-formats "$id"
else if test -n "$_flag_music"
   $ydl -x --id --format $_flag_format --no-playlist "$url"
	if test ! -n "$_flag_noplay"
   		mpv "/tmp/$id.m4a"
	end
else
	$ydl --id --format $_flag_format --no-playlist "$url"
	if test ! -n "$_flag_noplay"
		mpv "/tmp/$id.mp4"
	end
end

