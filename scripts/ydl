#! /usr/bin/fish

# video format (18)
set _flag_format 18

function usage
	echo "Usage:"
	echo (basename (status -f)) "[-f format] [-h] [-i] [-l] [-m] [url]" 
	echo "    f/format: set the format."
	echo "    h/help: show this help."
	echo "    i/info: list available formats."
	echo "    l/list: download the whole list."
	echo "    m/music: download music only."
	exit 1
end

argparse "l/list" "f/format=" "i/info" "m/music" "h/help" -- $argv
or usage

if test -n "$_flag_help"
	usage
end

if test -n "$argv[1]"
	set url $argv[1]
else
	set url (wl-paste)
end
set id (youtube-dl -q --no-playlist --get-id "$url")


cd /tmp/

if test -n "$_flag_list"
	youtube-dl --format $_flag_format  -o '%(playlist_index)s.mp4' "$url"
else if test -n "$_flag_info"
	youtube-dl --list-formats "$id"
else if test -n "$_flag_music"
   youtube-dl -x --id --format $_flag_format --no-playlist "$url"
   mpv "/tmp/$id.m4a"
else
	youtube-dl --id --format $_flag_format --no-playlist "$url"
	mpv "/tmp/$id.mp4"
end

